apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply from: dependenciesFilePath

android {
    compileSdkVersion projectInfo.compileSdkVersion

    defaultConfig {
        applicationId projectInfo.applicationId

        minSdkVersion projectInfo.minSdkVersion
        targetSdkVersion projectInfo.targetSdkVersion

        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary true

        testInstrumentationRunner "com.illiarb.tmdbexplorer.functional.AppRunner"
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            debuggable false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    signingConfigs {
        release {
            def propsFile = rootProject.file('keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))

                storeFile file(props['storeFile'])
                storePassword props['storePassword']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }
    }
}

dependencies {

    kapt deps.daggerCompiler

    implementation project(':core')
    implementation project(':core_ui')
    implementation project(':core_ui_image')
    implementation project(':core_storage')
    implementation project(':core_tools')
    implementation project(':service_analytics')
    implementation project(':service_tmdb')
    implementation project(':app_features')
    implementation project(':app_features_settings')
    implementation project(':debug')

    implementation deps.dagger
    implementation deps.timber
    implementation deps.firebaseCore
    implementation deps.navigation
    // Play core lib for downloading dynamic features
    implementation deps.playCore

    debugImplementation deps.leakCanary

    androidTestImplementation androidTestDeps.espressoIntents
    androidTestImplementation androidTestDeps.runner
    androidTestImplementation androidTestDeps.kakao
    androidTestImplementation androidTestDeps.extJunit
    androidTestImplementation androidTestDeps.uiAutomator
    androidTestImplementation androidTestDeps.kaspresso
}

apply plugin: 'com.google.gms.google-services'