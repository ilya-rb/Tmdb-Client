buildscript {

    ext.dependenciesFilePath = "$rootDir/dependencies.gradle"
    ext.versionsFilePath = "$rootDir/versions.gradle"

    apply from: versionsFilePath
    apply from: dependenciesFilePath

    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }

    dependencies {
        classpath gradlePlugins.android
        classpath gradlePlugins.kotlin
        classpath gradlePlugins.versions
        classpath gradlePlugins.jacoco
        classpath gradlePlugins.dexcount
        classpath gradlePlugins.buildscan
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC12"
}

allprojects {

    apply from: "$rootDir/code_quality_tools/detekt.gradle"

    repositories {
        google()
        maven { url "https://jitpack.io" }
        jcenter()
    }

    tasks.withType(JavaCompile).all {
        options.compilerArgs += [
                '-Adagger.formatGeneratedSource=disabled',
                '-Adagger.gradle.incremental=enabled'
        ]
    }

    afterEvaluate {
        extensions.findByName('kapt')?.arguments {
            arg("dagger.formatGeneratedSource", "disabled")
            arg("dagger.gradle.incremental", "enabled")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: 'com.gradle.build-scan'

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-servie'
    licenseAgree = 'yes'
}